<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

	<xsl:template match="/">
		<xsl:apply-templates select="CHAPTER"/>
	</xsl:template>

	<!-- The DocBook SGML generated by Lyx doesn't include meta
		information for version even though
		DocBook supports those constructs. Thus for this template we just
		fill in the structure and leave the details blank.

		There's nothing too wrong with hardcoding details for those
		elements here though.
	-->
	<xsl:template match="CHAPTER">
		<guide>
			<version></version>
			<xsl:apply-templates/>
		</guide>
	</xsl:template>

	<xsl:template match="AUTHOR">
		<author><xsl:value-of select="."/></author>
	</xsl:template>

	<xsl:template match="DATE">
		<date><xsl:value-of select="."/></date>
	</xsl:template>

	<xsl:template match="ABSTRACT">
		<abstract><xsl:value-of select="."/></abstract>
	</xsl:template>

	<xsl:template match="TITLE">
		<title><xsl:value-of select="."/></title>
	</xsl:template>

	<xsl:template match="SECT1">
		<chapter>
			<xsl:apply-templates select="TITLE"/>
			<body>
				<xsl:apply-templates select="PARA|PROGRAMLISTING"/>
			</body>
		<xsl:apply-templates select="SECT2"/>
		</chapter>
	</xsl:template>

	<xsl:template match="SECT2">
		<section>
			<xsl:apply-templates select="TITLE"/>
			<body>
				<xsl:apply-templates select="PARA|PROGRAMLISTING|SECT3"/>
			</body>
		</section>
	</xsl:template>

	<xsl:template match="PARA">
		<p>
			<xsl:apply-templates />
		</p>
	</xsl:template>

	<xsl:template match="ULINK">
		<uri link="{@URL}"><xsl:value-of select="."/></uri>
	</xsl:template>

	<xsl:template match="PROGRAMLISTING">
		<pre><xsl:value-of select="."/></pre>
	</xsl:template>

	<xsl:template match="EMPHASIS">
		<i><xsl:value-of select="."/></i>
	</xsl:template>

	<xsl:template	match="SECT3">
		<subsection>
			<xsl:apply-templates select="TITLE"/>
			<body>
				<xsl:apply-templates select="PARA|PROGRAMLISTING"/>
			</body>
		</subsection>
	</xsl:template>

</xsl:stylesheet>
