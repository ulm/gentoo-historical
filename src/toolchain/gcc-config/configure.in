AC_PREREQ(2.59)
AC_INIT(eselect-compiler, 2.0.0_rc2, toolchain@gentoo.org)
AM_INIT_AUTOMAKE
AC_CONFIG_HEADER([config.h])

dnl Checks for programs.
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl AC_ENABLE_SHARED
dnl AC_DISABLE_STATIC
dnl Next four lines is a hack to prevent libtool checking for CXX/F77
dnl m4_undefine([AC_PROG_CXX])
dnl m4_defun([AC_PROG_CXX],[])
dnl m4_undefine([AC_PROG_F77])
dnl m4_defun([AC_PROG_F77],[])
dnl AC_PROG_LIBTOOL

# Check for compiler features
AC_C_INLINE

# Check for functions
AC_FUNC_MKDIR

# Check for types
AC_TYPE_SIZE_T

# Let the user override where files are located
AC_ARG_WITH(eselect-mod-dir,
        AS_HELP_STRING([--with-eselect-mod-dir=DIR],[directory where we should install the eselect modules (Default: DATADIR/eselect/modules)]),
        eselectmoddir="${withval}",
        eselectmoddir="${datadir}/eselect/modules")
AC_DEFINE_DIR(ESELECT_MOD_DIR,eselectmoddir,[Directory where we install eselect modules])

AC_ARG_WITH(config-dir,
        AS_HELP_STRING([--with-config-dir=DIR],[directory where the global config files are located (Default: /etc/eselect/compiler)]),
        configdir="${withval}",
        configdir="/etc/eselect/compiler")
AC_DEFINE_DIR(CONFIGURATION_DIR,configdir,[Directory where the global config files are located])

AC_ARG_WITH(user-config-dir,
        AS_HELP_STRING([--with-user-config-dir=DIR],[directory in the users home where the user-overrides are located (Default: .compiler-config)]),
        userconfigdir="${withval}",
        userconfigdir=".compiler-config")
AC_DEFINE_DIR(USER_CONFIGURATION_DIR,userconfigdir,[Directory in the users home where the user-overrides are located])

evallibexecdir="${libexecdir}"
AC_DEFINE_DIR(EVALLIBEXEC_DIR,evallibexecdir,[Eval'd libexecdir])

AC_CHECK_HEADERS([alloca.h])
AC_CHECK_FUNCS([strndup strnlen])

AC_OUTPUT([
	Makefile
	conf/Makefile
	src/Makefile
	src/compiler-wrapper/Makefile
	src/libcommon/Makefile
	src/profile-manager/Makefile
	src/profile-manager/gcc-config
	src/profile-manager/compiler.eselect
])
